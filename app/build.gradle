apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply from: '../quality/tasks.gradle'

android {
  compileSdkVersion androidConfig.compileSdkVersion
  defaultConfig {
    applicationId androidConfig.applicationId
    minSdkVersion androidConfig.minSdkVersion
    targetSdkVersion androidConfig.targetSdkVersion
    versionCode androidConfig.versionCode
    versionName androidConfig.versionName
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  buildTypes.each {
    it.buildConfigField 'String', 'API_KEY', apiKey
    it.buildConfigField 'String', 'API_LANGUAGE_CODE', apiLanguageCode
  }

  compileOptions {
    sourceCompatibility 1.8
    targetCompatibility 1.8
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8.toString()
  }

  lintOptions {
    warningsAsErrors true
    abortOnError true
    disable 'Overdraw'
    enable 'SyntheticAccessor'
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    kotlinOptions.freeCompilerArgs += ["-Xallow-result-return-type"]
  }
}

androidExtensions {
  experimental = true
}

dependencies {
  implementation deps.picasso
  implementation deps.kotlin
  implementation deps.activity
  implementation deps.fragment
  implementation deps.appcompat
  implementation deps.card_view
  implementation deps.recycler_view
  implementation deps.constraint_layout
  implementation deps.material
  implementation deps.material_dialogs
  implementation deps.coroutines_core
  implementation deps.coroutines_android

  // ktx
  implementation deps.fragment_ktx
  implementation deps.core_ktx
  implementation deps.lifecycle_viewmodel_ktx

  // ViewModel and LiveData
  implementation deps.lifecycle_extensions

  // Networking
  implementation deps.okhttp
  implementation deps.okhttp_urlconnection
  implementation deps.okhttp_logging
  implementation deps.retrofit
  implementation deps.retrofit_converter_gson

  // Dependency Injection
  kapt deps.hilt_compiler
  kapt deps.hilt_androidx_compiler
  implementation deps.hilt
  implementation deps.hilt_viewmodel

  testImplementation testDeps.mockk
  testImplementation testDeps.junit
  testImplementation testDeps.hamcrest
  testImplementation testDeps.arch_core_testing
  testImplementation testDeps.assertj
  testImplementation testDeps.coroutines_test
  testImplementation deps.coroutines_core
}
